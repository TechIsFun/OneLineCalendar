apply plugin: 'com.android.application'
apply plugin: 'com.facebook.testing.screenshot'
apply plugin: 'shot'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    defaultConfig {
        applicationId "com.github.techisfun.onelinecalendar.app"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.gitVersionCode
        versionName rootProject.ext.gitVersionName
        testInstrumentationRunner "com.github.techisfun.onelinecalendar.CustomAndroidTestRunner"

        // set the name of the generated apk in the form
        // <projectName>-<versionName>.apk
        setProperty("archivesBaseName", "$project.name-$versionName")
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        disable 'NewApi' // gradle build will compile code use `Objects.requireNonNull()` for lambda
        abortOnError true
        xmlReport true
        xmlOutput project.file('build/reports/lint/lint-report.xml')
        htmlReport true
        htmlOutput project.file('build/reports/lint/lint-report.html')
        lintConfig file("${project.rootDir}/quality/lint/lint.xml")
    }
    testOptions.unitTests {
        all {
            testLogging {
                events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
                outputs.upToDateWhen { false }
                showStandardStreams = true
            }
            jvmArgs '-noverify'
        }
        returnDefaultValues = true
    }
}

dependencies {

    compile project(':library')

    androidTestCompile(rootProject.ext.testLibraries.espresso, {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    compile rootProject.ext.appLibraries.supportAppCompat
    //compile rootProject.ext.appLibraries.constraintLayout
    //compile rootProject.ext.appLibraries.timber
    //compile rootProject.ext.appLibraries.butterknife
    annotationProcessor rootProject.ext.appLibraries.butterknifeProcessor

    testCompile rootProject.ext.testLibraries.junit
    testCompile rootProject.ext.testLibraries.mockito
}

shot {
    appId = 'com.github.techisfun.onelinecalendar.app.debug'
}

